group 'com.sao.learning'
version '1.0-SNAPSHOT'

def grpcVersion = '1.3.0'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
        classpath("com.google.protobuf:protobuf-gradle-plugin:0.8.0")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'

protobuf {
    protoc {
        artifact='com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        grpc {
            artifact="io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // To generate deprecated interfaces and static bindService method,
                // turn the enable_deprecated option to true below:
                option 'enable_deprecated=false'
            }
        }
    }
}

idea {
    module {
        // Not using generatedSourceDirs because of
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs+=file("${projectDir}/build/generated/source/proto/main/java")
        sourceDirs+=file("${projectDir}/build/generated/source/proto/main/grpc")
    }
}

jar {
    baseName='learning-google-assistant'
    version='1.0-SNAPSHOT'
}

sourceCompatibility=1.8
targetCompatibility=1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter")
    compile("io.grpc:grpc-netty:${grpcVersion}")
    compile("io.grpc:grpc-protobuf:${grpcVersion}")
    compile("io.grpc:grpc-stub:${grpcVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("junit:junit")
}
